import os
import json

from dpipe.config import get_paths


PATH_TO_SCRIPTS = get_paths()['scripts']

TRAIN_SCRIPT = os.path.join(PATH_TO_SCRIPTS, 'train_model.py')
PREDICT_SCRIPT = os.path.join(PATH_TO_SCRIPTS, 'predict.py')
TRANSFORM = os.path.join(PATH_TO_SCRIPTS, 'transform.py')
COMPUTE_DICES = os.path.join(PATH_TO_SCRIPTS, 'compute_dices.py')

CONFIG = '../config.json'
SAVED_MODEL = 'model'
TF_LOG = 'train_logs'

TRAIN_IDS = 'train_ids.json'
VAL_IDS = 'val_ids.json'
TEST_IDS = 'test_ids.json'

TEST_DICES = 'test_dices.json'

rule all:
    input:
        SAVED_MODEL, TEST_DICES

rule train_model:
    input:
        TRAIN_IDS, VAL_IDS
    output:
        SAVED_MODEL, TF_LOG
    shell:
        'python {TRAIN_SCRIPT} -cp {CONFIG} --train_ids_path {TRAIN_IDS} --val_ids_path {VAL_IDS} --log_path {TF_LOG} --save_model_path {SAVED_MODEL}'

rule predict:
    input:
        SAVED_MODEL,
        ids = '{sample}_ids.json'
    output:
        predictions = '{sample}_segm_pred_proba'
    shell:
        'python {PREDICT_SCRIPT} -cp {CONFIG} --ids_path {input.ids} --predictions_path {output.predictions} --restore_model_path {SAVED_MODEL}'

rule predictions2msegm:
    input:
        predictions = '{sample}_segm_pred_proba'
    output:
        predictions = '{sample}_msegm_pred'
    shell:
        'python {TRANSFORM} -cp {CONFIG} --input_path {input.predictions} --output_path {output.predictions} --transform prob2msegm'

rule compute_dices:
    input:
        predictions = '{sample}_msegm_pred'
    output:
        dices = '{sample}_dices.json'
    shell:
        'python {COMPUTE_DICES} -cp {CONFIG} --predictions_path {input.predictions} --dices_path {output.dices}'
